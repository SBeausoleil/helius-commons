import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id "java-library"
    id "com.vanniktech.maven.publish" version "0.30.0"
    id "signing"
    id "me.champeau.jmh" version "0.7.2"
    id "java-test-fixtures"
}

group = "systems.helius"
version = "0.3.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation group: "jakarta.annotation", name: "jakarta.annotation-api", version: "3.0.0"

    testImplementation platform("org.junit:junit-bom:5.10.0")
    testImplementation "org.junit.jupiter:junit-jupiter"

    testFixturesApi group: "io.github.sbeausoleil", name: "factorium", version: "2.1.0"
    testFixturesImplementation group: "jakarta.annotation", name: "jakarta.annotation-api", version: "3.0.0"
    testFixturesCompileOnly 'org.projectlombok:lombok:1.18.36'
    testFixturesAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

    jmhImplementation group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.37'
    jmhAnnotationProcessor  group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.37'
}

test {
    useJUnitPlatform()
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    coordinates(group, rootProject.name, version)

    pom {
        name = "Helius Commons"
        description = "A common utilities library with a focus on reflective programming."
        url = "https://github.com/SBeausoleil/helius-commons"
        licenses {
            license {
                name = "MIT Licence"
                url = "https://github.com/SBeausoleil/helius-commons/blob/master/LICENSE.md"
            }
        }
        developers {
            developer {
                id = "samuelb"
                name = "Samuel Beausoleil"
                email = "sbeausoleil_dev@hotmail.com"
                organization = "Independent"
            }
        }
        scm {
            connection = "scm:git:git://github.com/SBeausoleil/helius-commons.git"
            developerConnection = "scm:git:ssh://github.com:SBeausoleil/helius-commons.git"
            url = "https://github.com/SBeausoleil/helius-commons"
        }
    }

    signAllPublications()
    signing {
        useGpgCmd()
    }
}